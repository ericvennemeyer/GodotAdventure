class_name Player
extends CharacterBody2D


@export var move_speed: float = 100.0
@export var push_strength: float = 300.0

@onready var animated_sprite_2d: AnimatedSprite2D = $AnimatedSprite2D


func _ready() -> void:
	position = SceneManager.player_spawn_position
	#Engine.max_fps = 30


func _process(delta: float) -> void:
	pass


func _physics_process(delta: float) -> void:
	var move_vector: Vector2 = Input.get_vector("move_left", "move_right", "move_up", "move_down")
	velocity = move_vector * move_speed
	
	if velocity.x > 0:
		animated_sprite_2d.play("move_right")
	elif velocity.x < 0:
		animated_sprite_2d.play("move_left")
	elif velocity.y > 0:
		animated_sprite_2d.play("move_down")
	elif velocity.y < 0:
		animated_sprite_2d.play("move_up")
	else:
		animated_sprite_2d.stop()
	
	# Get the last collision
	# If there was a collision, get the colliding node
	# If colliding node was Block, push the block
	var collision: KinematicCollision2D = get_last_slide_collision()
	if collision:
		var collider_node = collision.get_collider()
		if collider_node.is_in_group("pushable"):
			var collision_normal: Vector2 = collision.get_normal()
			collider_node.apply_central_force(-collision_normal * push_strength)
	
	
	move_and_slide()
